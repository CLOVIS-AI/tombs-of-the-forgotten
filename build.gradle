/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'ivan' at '03/05/18 16:15' with Gradle 3.2.1
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.2.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'javafx-gradle-plugin'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
}

getVersion()

private void getVersion(){
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--candidates=30'
        standardOutput = stdout
    }
    version stdout.toString()[0..-2].replaceFirst("-\\d+-", "-")
}

mainClassName = 'com.cc.tof.ToF'

run {
    standardInput = System.in
}

sourceSets {
    main {
        java {
            srcDirs = ['ToF/src']
        }
        resources {
            srcDirs = ['ToF/resources']
            excludes = ['**/*.java']
        }
    }
    test {
        java {
            srcDirs = ['ToF/test']
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Main-Class': 'com.cc.tof.ToF'
    }
}

buildscript {
    dependencies {
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
    }
    repositories {
        mavenCentral()
    }
}

jfx {
    mainClass = mainClassName
    vendor = 'Canet-Chourouq'
    verbose = true
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xdiags:verbose"
    }
}
